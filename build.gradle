apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = "com.github.adrninistrator"
version = "0.0.1"
def projectName = "java-behavior-control"
description = "${projectName}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

def defaultEncoding = 'UTF-8'

[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

project.buildDir = 'build'

configurations {

    implementation.exclude module: 'slf4j-log4j12'
    implementation.exclude module: 'log4j'
    implementation.exclude module: 'commons-logging'
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }

        resources {
            srcDir 'src/main/resources'
        }
    }

    test {
        java {
            srcDir 'src/test/java'
        }

        resources {
            srcDir 'src/test/resources'
        }
    }
}

List slf4j = [
        "org.slf4j:jcl-over-slf4j:1.7.30"
]

List apache_commons = [
        "commons-io:commons-io:2.7"
]

List junit = ["junit:junit:4.13"]

List powermock = [
        "org.powermock:powermock-module-junit4:2.0.5",
        "org.powermock:powermock-api-mockito2:2.0.5"
]

List mockito = [
        "org.mockito:mockito-core:3.2.4"
]

List log4j = [
        "org.apache.logging.log4j:log4j-api:2.13.3",
        "org.apache.logging.log4j:log4j-core:2.13.3",
        "org.apache.logging.log4j:log4j-slf4j-impl:2.13.3",
]

dependencies {
    implementation slf4j, apache_commons
    testImplementation log4j, junit, powermock, mockito
}

compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

processResources {
    setDuplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

task createProject {
    doFirst {
        sourceSets*.java.srcDirs*.each { it.mkdirs() }
        sourceSets*.resources.srcDirs*.each { it.mkdirs() }
    }
}

task sourcesJar(type: Jar) {
    doFirst {
        archiveClassifier.set('sources')
        from sourceSets.main.allJava
    }
}

task javadocJar(type: Jar) {
    doFirst {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version "${version}"
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "java-behavior-control"
                description = "Monitor and prevent unexpected behavior of Java programs."
                url = "https://github.com/Adrninistrator/java-behavior-control"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "Adrninistrator"
                        name = "zhengyiee"
                        email = "zhengyiee@qq.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/Adrninistrator/java-behavior-control.git"
                    developerConnection = "scm:git:https://github.com/Adrninistrator/java-behavior-control.git"
                    url = "https://github.com/Adrninistrator/java-behavior-control"
                }
            }
        }
    }
    repositories {
        maven {
            name 'sonatypeRepository'
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
        maven {
            name = 'sonatypeSnapshotRepository'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}